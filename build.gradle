buildscript {
  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
  }

  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:2.2.0.RELEASE"
    classpath "io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE"
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+"
  }
}

apply plugin: "idea"
apply plugin: "eclipse"

group "kr.lul.kobalttown"
version "0.0.1.SNAPSHOT"

allprojects {
  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
    maven {
      url "https://dl.bintray.com/justburrow/snapshot"
    }
    maven {
      url "https://dl.bintray.com/justburrow/release"
    }
  }
}

subprojects {
  apply plugin: "java"
  apply plugin: "org.springframework.boot"
  apply plugin: "io.spring.dependency-management"
  apply plugin: "com.jfrog.bintray"

  sourceCompatibility = 11
  targetCompatibility = 11

  dependencyManagement {
    imports {
      mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }

    def commonVersion = "0.0.1.SNAPSHOT"
    dependencies {
      dependency "kr.lul.common:common-util:$commonVersion"
      dependency "kr.lul.common:common-web:$commonVersion"

      dependency "kr.lul.support.spring:support-spring-data-jpa:0.0.1"
      dependency "kr.lul.support.spring:support-spring-security:0.0.1"

      dependency "org.assertj:assertj-core:3.14.0"
      dependency "org.apache.commons:commons-lang3:3.9"
    }
  }

  dependencies {
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.assertj:assertj-core"
    testImplementation "org.apache.commons:commons-lang3"
  }

  jar.enabled = true
  bootJar.enabled = false

  bintray {
    user = bintrayUser
    key = bintrayApiKey

    pkg {
      repo = project.version.toString().endsWith("RELEASE")
          ? "release"
          : "snapshot"
      name = project.name
      desc = project.description

      websiteUrl = "https://blog.lul.kr"
      issueTrackerUrl = "https://github.com/JustBurrow/kobalttown/issues"
      vcsUrl = "https://github.com/JustBurrow/kobalttown.git"
      licenses = ["Apache-2.0"]

      publicDownloadNumbers = true
      publish = true
      override = project.version.toString().endsWith("SNAPSHOT")
      configurations = ["archives"]

      githubRepo = "JustBurrow/kobalttown"
      githubReleaseNotesFile = "README.md"

      version {
        name = project.version
        desc = project.description
      }
    }
  }
}

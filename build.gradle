buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:2.2.0.RELEASE"
    classpath "io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE"
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+"
  }
}

apply plugin: "idea"
apply plugin: "eclipse"

allprojects {
  group "kr.lul.kobalttown"
  version "0.0.1.SNAPSHOT"

  repositories {
    jcenter()
    mavenCentral()
    maven {
      url "https://dl.bintray.com/justburrow/kr.lul"
    }
  }
}

subprojects {
  apply plugin: "java"
  apply plugin: "org.springframework.boot"
  apply plugin: "io.spring.dependency-management"
  apply plugin: "com.jfrog.bintray"

  sourceCompatibility = 13
  targetCompatibility = 13

  repositories {
    mavenCentral()
    jcenter()
  }

  dependencyManagement {
    imports {
      mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }

    dependencies {
      dependency "kr.lul.common:common-util:0.0.1"
      dependency "kr.lul.common:common-web:0.0.1"

      dependency "kr.lul.support.spring:support-spring-data-jpa:0.0.1"
      dependency "kr.lul.support.spring:support-spring-security:0.0.1"

      dependency "org.assertj:assertj-core:3.14.0"
      dependency "org.apache.commons:commons-lang3:3.9"
    }
  }

  dependencies {
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
      exclude group: "org.junit.jupiter", module: "junit-jupiter"
    }
    testImplementation "org.assertj:assertj-core"
    testImplementation "org.apache.commons:commons-lang3"
  }

  jar.enabled = true
  bootJar.enabled = false

  bintray {
    user = bintrayUser
    key = bintrayApiKey

    pkg {
      repo = project.group
      name = project.name

      licenses = ["Apache-2.0"]
      publicDownloadNumbers = true
      publish = true
      configurations = ["archives"]

      vcsUrl = "https://github.com/JustBurrow/kobalttown.git"
      githubRepo = "JustBurrow/kobalttown"
      githubReleaseNotesFile = "README.md"

      version {
        name = project.version
      }
    }
  }
}
